
&НаКлиенте
Процедура СоздатьШК(Команда)
	
	ОчиститьТабДок();
	ОлдПрогресс = 0;
	Прогресс    = 0;
	ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Прогресс);
	
	МассивОшибок = Новый Массив;
	
	//Обработка штрихкодов из справочника "ШтрихкодыУпаковок"
	ШКСправочник = ПолучитьШКСправочник();
	
	мСч = 0;
	мКолво = ШКСправочник.Количество();
	
	Для каждого Эл из ШКСправочник Цикл
		ОписаниеОшибки = СоздатьШК_Справочник(Эл);
		Если ОписаниеОшибки <> "" Тогда
			МассивОшибок.Добавить(ОписаниеОшибки);
		КонецЕсли;
		
		мСч = мСч + 1;
		Прогресс = мСч / мКолво * 25;
		Если Прогресс > ОлдПрогресс Тогда
			ОлдПрогресс = Прогресс;
			ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Прогресс);
		КонецЕсли;
		
	КонецЦикла;	
	
	//Обработка всех файлов ЭДО, выделяем нужные с марками
	Папка = КаталогВременныхФайлов() + "temp_mp\";
	СоздатьКаталог(Папка);
	
	ФайлыЭДО = ПолучитьФайлыЭДО();
	
	ЭлФайл      = Неопределено;
	ЭлПартнер   = Неопределено;
	НужныеФайлы = Новый Массив;
	
	й   = 1;
	мСч = 0;
	
	мКолво = ФайлыЭДО.Количество();
	Для каждого Эл из ФайлыЭДО Цикл
		
		Эл.Свойство("Файл",    ЭлФайл);
		Эл.Свойство("Партнер", ЭлПартнер);
		
		ДД = ПолучитьДД(ЭлФайл);
		
		Если ДД = Неопределено Тогда Продолжить; КонецЕсли;
		
		ДД.Записать(Папка + "_temp.txt");
		
		Текст = Новый ЧтениеТекста;
		Текст.Открыть(Папка + "_temp.txt");
		Стр = Текст.Прочитать();
		Если СтрНайти(Стр, "<КИЗ>") > 0 Тогда
			
			ИмяФайла = Папка + СтрЗаменить(Формат(й, "ЧЦ=8; ЧРГ='_'; ЧВН="), "_", "") + ".xml";
			
			ДД.Записать(ИмяФайла);
			
			Структ = Новый Структура;
			Структ.Вставить("Файл",    ИмяФайла);
			Структ.Вставить("Партнер", ЭлПартнер);
			НужныеФайлы.Добавить(Структ);
			
			й = й + 1;
			
		КонецЕсли;
		
		Текст.Закрыть();
		
		мСч      = мСч + 1;
		Прогресс = мСч / мКолво * 25 + 25;
		
		Если Прогресс > ОлдПрогресс Тогда
			ОлдПрогресс = Прогресс;
			ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Прогресс);
		КонецЕсли;
		
	КонецЦикла;
	
	УдалитьФайлы(Папка + "_temp.txt");
	
	//Вытаскиваем данные из нужных файлов
	Данные   = Новый Массив;
	МассивШК = Новый Массив;
	
	СейчасКИЗ       = Ложь;
	ТекПартнер      = Неопределено;
	ТекНоменклатура = "";
	ИмяФайла        = "";
	
	мСч    = 0;
	мКолво = НужныеФайлы.Количество();
	
	Для каждого Эл из НужныеФайлы Цикл
		
		Эл.Свойство("Файл",    ИмяФайла);
		Эл.Свойство("Партнер", ТекПартнер);
		
		Чтение = Новый ЧтениеXML;
		Чтение.ОткрытьФайл(ИмяФайла);
		Пока Чтение.Прочитать() Цикл
			
			Если Чтение.Имя = "СведТов" И Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда //строка с номенклатурой
				ТекНоменклатура = Чтение.ЗначениеАтрибута("НаимТов");
			КонецЕсли;	
			
			Если СейчасКИЗ Тогда
				Штрихкод = Сред(Чтение.Значение, 4, 13);
				Если МассивШК.Найти(Штрихкод)=Неопределено Тогда
					МассивШК.Добавить(Штрихкод);
					
					Структ = Новый Структура;
					Структ.Вставить("Партнер",      ТекПартнер);
					Структ.Вставить("Номенклатура", ТекНоменклатура);
					Структ.Вставить("Штрихкод",     Штрихкод);
					Данные.Добавить(Структ);
				КонецЕсли;	
				СейчасКИЗ = Ложь;
			ИначеЕсли Чтение.Имя = "КИЗ" И Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда //следующий КИЗ
				СейчасКИЗ = Истина;
			КонецЕсли;	
			
		КонецЦикла;
		
		Чтение.Закрыть();
		
		мСч = мСч + 1;
		Прогресс = мСч / мКолво * 25 + 50;
		Если Прогресс > ОлдПрогресс Тогда
			ОлдПрогресс = Прогресс;
			ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Прогресс);
		КонецЕсли;	
	КонецЦикла;
	
	//создаем штрихкоды на сервере
	мСч = 0;
	мКолво = Данные.Количество();
	Для каждого Эл из Данные Цикл
		ОписаниеОшибки = СоздатьШК_ЭДО(Эл);
		Если ОписаниеОшибки <> "" Тогда
			МассивОшибок.Добавить(ОписаниеОшибки);
		КонецЕсли;
		
		мСч = мСч + 1;
		Прогресс = мСч / мКолво * 25 + 75;
		Если Прогресс > ОлдПрогресс Тогда
			ОлдПрогресс = Прогресс;
			ЭтаФорма.ОбновитьОтображениеДанных(Элементы.Прогресс);
		КонецЕсли;	
	КонецЦикла;	
	
	//Выводим протокол
	ВывестиПротокол(МассивОшибок);
	
	ТабДок.ОтображатьЗаголовки = Ложь;
	ТабДок.ОтображатьСетку     = Ложь;
	
	Прогресс = 100;
	УдалитьФайлы(Папка);
	
КонецПроцедуры

////////////////////////////////////////

&НаСервере
Функция ПолучитьШКСправочник()
	
	Мас = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("ТипУпаковки", Перечисления.ТипыУпаковок.МаркированныйТовар);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВЫБОР
				   |		КОГДА ШтрихкодыУпаковокТоваров.Номенклатура.ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)
				   |			ТОГДА ВЫБОР
	               |					КОГДА ПОДСТРОКА(ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода, 2, 5) = ""00000""
	               |						ТОГДА ПОДСТРОКА(ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода, 7, 8)
	               |					ИНАЧЕ ПОДСТРОКА(ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода, 2, 13)
	               |				КОНЕЦ
	               |		ИНАЧЕ ПОДСТРОКА(ШтрихкодыУпаковокТоваров.ЗначениеШтрихкода, 6, 13)
	               |	КОНЕЦ КАК ШК,
	               |	ШтрихкодыУпаковокТоваров.Номенклатура КАК Номенклатура,
				   |	ШтрихкодыУпаковокТоваров.Характеристика КАК Характеристика,
				   |	ШтрихкодыУпаковокТоваров.Упаковка КАК Упаковка
	               |ПОМЕСТИТЬ ВремТабл
	               |ИЗ
	               |	Справочник.ШтрихкодыУпаковокТоваров КАК ШтрихкодыУпаковокТоваров
	               |ГДЕ
	               |	ШтрихкодыУпаковокТоваров.ТипУпаковки = &ТипУпаковки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ВремТабл.ШК КАК ШК,
	               |	ВремТабл.Номенклатура КАК Номенклатура,
				   |	ВремТабл.Характеристика КАК Характеристика,
				   |	ВремТабл.Упаковка КАК Упаковка
	               |ИЗ
	               |	ВремТабл КАК ВремТабл";
	
	Если Метаданные.Имя = "УправлениеТорговлей" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		               "ВидПродукцииИС = ЗНАЧЕНИЕ(Перечисление.ВидыПродукцииИС.Табак)",
					   "ОсобенностьУчета = ЗНАЧЕНИЕ(Перечисление.ОсобенностиУчетаНоменклатуры.ТабачнаяПродукция)");
	КонецЕсли;
	
	Рез = Запрос.Выполнить().Выбрать();
	Пока Рез.Следующий() Цикл
		Структ = Новый Структура;
		Структ.Вставить("Штрихкод",       Рез.ШК);
		Структ.Вставить("Номенклатура",   Рез.Номенклатура);
		Структ.Вставить("Характеристика", Рез.Характеристика);
		Структ.Вставить("Упаковка",       Рез.Упаковка);
		Мас.Добавить(Структ);
	КонецЦикла;	
		
	Возврат Мас;
	
КонецФункции

&НаСервере
Функция СоздатьШК_Справочник(Стр)
	
	ОписаниеОшибки = "";
	
	НаименованиеКонфигурации = Метаданные.Имя;
	
	Штрихкод       = Неопределено; 
	Номенклатура   = Неопределено;
	Характеристика = Неопределено;
	Упаковка       = Неопределено;
	
	Стр.Свойство("Штрихкод",       Штрихкод);
	Стр.Свойство("Номенклатура",   Номенклатура);
	Стр.Свойство("Характеристика", Характеристика);
	Стр.Свойство("Упаковка",       Упаковка);
	
	Набор = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
	
	Набор.Отбор.Штрихкод.Установить(Штрихкод);
	
	Если НаименованиеКонфигурации = "УправлениеТорговлей" Тогда
	    		
		Набор.Прочитать();
		
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.Штрихкод       = Штрихкод;
			Запись.Номенклатура   = Номенклатура;
			Запись.Характеристика = Характеристика;
			Запись.Упаковка       = Упаковка;
			Набор.Записать();
			ОписаниеОшибки = "(0)Для товара """ + Номенклатура + """" + ?(Характеристика = Неопределено, "", " " + Характеристика) + "" + ?(Упаковка = Неопределено, "", " " + Упаковка) + " установлен штрихкод """ + СокрЛП(Штрихкод) + """";
		Иначе
			Если Набор[0].Номенклатура<>Номенклатура Тогда
				ОписаниеОшибки = "(1)Штрихкод """ + СокрЛП(Штрихкод) + """ уже назначен для товара """ + Набор[0].Номенклатура + """" 
				                 + ?(ЗначениеЗаполнено(Набор[0].Характеристика), " с характеристикой - " + Набор[0].Характеристика, ""
								 + ?(ЗначениеЗаполнено(Набор[0].Упаковка), " и упаковки - " + Набор[0].Упаковка, ""));
			КонецЕсли;	
		КонецЕсли;
		
	ИначеЕсли НаименованиеКонфигурации = "УправлениеНебольшойФирмой" Тогда
		
		Если Номенклатура <> Неопределено Тогда
			Набор.Отбор.Номенклатура.Установить(Номенклатура);
		КонецЕсли;
		Если Характеристика <> Неопределено Тогда
			Набор.Отбор.Характеристика.Установить(Характеристика);
		КонецЕсли;
		Если Упаковка <> Неопределено Тогда
			Набор.Отбор.ЕдиницаИзмерения.Установить(?(ТипЗнч(Упаковка) = Тип("СправочникСсылка.ЕдиницыИзмерения"), Упаковка, Справочники.ЕдиницыИзмерения.ПустаяСсылка()));
		КонецЕсли;
		
		Набор.Прочитать();
		
		Если Набор.Количество() = 0 Тогда
			Запись = Набор.Добавить();
			Запись.Штрихкод         = Штрихкод;
			Запись.Номенклатура     = Номенклатура;
			Запись.Характеристика   = Характеристика;
			Запись.ЕдиницаИзмерения = Упаковка;
			Набор.Записать();
			ОписаниеОшибки = "(0)Для товара """ + Номенклатура + """" + ?(Характеристика = Неопределено, "", " " + Характеристика) + "" + ?(Упаковка = Неопределено, "", " " + Упаковка) + " установлен штрихкод """ + СокрЛП(Штрихкод) + """";
		Иначе
			Если Набор[0].Номенклатура<>Номенклатура Тогда
				ОписаниеОшибки = "(1)Штрихкод """ + СокрЛП(Штрихкод) + """ уже назначен для товара """ + Набор[0].Номенклатура + """" 
				                 + ?(ЗначениеЗаполнено(Набор[0].Характеристика), " с характеристикой - " + Набор[0].Характеристика, ""
								 + ?(ЗначениеЗаполнено(Набор[0].ЕдиницаИзмерения), " и упаковки - " + Набор[0].ЕдиницаИзмерения, ""));
			КонецЕсли;	
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

////////////////////////////////////////

&НаСервере
Функция ПолучитьФайлыЭДО()
	
	Мас = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.Параметры.Вставить("НаправлениеЭД", Перечисления.НаправленияЭД.Входящий);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЭДПрисоединенныеФайлы.Ссылка КАК Файл,
	               |	ЭДПрисоединенныеФайлы.ВладелецФайла.Контрагент КАК Партнер
	               |ИЗ
	               |	Справочник.ЭДПрисоединенныеФайлы КАК ЭДПрисоединенныеФайлы
	               |ГДЕ
	               |	ЭДПрисоединенныеФайлы.НаправлениеЭД = &НаправлениеЭД
	               |	И ЭДПрисоединенныеФайлы.ВладелецФайла.СодержитДанныеОМаркируемыхТоварах";
	
	Если Метаданные.Имя = "УправлениеТорговлей" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
		               "Контрагент КАК Партнер",
					   "Контрагент.Партнер КАК Партнер");
	КонецЕсли;
	
	Рез = Запрос.Выполнить().Выбрать();
	Пока Рез.Следующий() Цикл
		Структ = Новый Структура;
		Структ.Вставить("Файл",    Рез.Файл);
		Структ.Вставить("Партнер", Рез.Партнер);
		Мас.Добавить(Структ);
	КонецЦикла;	
		
	Возврат Мас;
	
КонецФункции

&НаСервере
Функция ПолучитьДД(Стр)
	Возврат РаботаСФайлами.ДвоичныеДанныеФайла(Стр,Ложь);
КонецФункции

&НаСервере
Функция СоздатьШК_ЭДО(Стр)
	
	ОписаниеОшибки="";
	
	Штрихкод          = Неопределено; 
	ТекстНоменклатура = Неопределено; 
	Партнер           = Неопределено;
	
	Стр.Свойство("Штрихкод",     Штрихкод);
	Стр.Свойство("Номенклатура", ТекстНоменклатура);
	Стр.Свойство("Партнер",      Партнер);
	
	//Ищем в номенклатуре поставщиков
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НоменклатураКонтрагентовБЭД.Номенклатура КАК Номенклатура
	               |ИЗ
	               |	РегистрСведений.НоменклатураКонтрагентовБЭД КАК НоменклатураКонтрагентовБЭД
	               |ГДЕ
	               |	НоменклатураКонтрагентовБЭД.Владелец = &Партнер
	               |	И НоменклатураКонтрагентовБЭД.Наименование = &Наименование";
	
	Запрос.Параметры.Вставить("Партнер",      Партнер);
	Запрос.Параметры.Вставить("Наименование", ТекстНоменклатура);
	
	Рез = Запрос.Выполнить().Выбрать();
	Если Рез.Следующий() Тогда
		Номенклатура = Рез.Номенклатура;
	Иначе	
		ОписаниеОшибки = "(2)Не установлено соответствие для номенклатуры """+ТекстНоменклатура+""" поставщика """+СокрЛП(Партнер)+"""";
		Возврат ОписаниеОшибки;
	КонецЕсли;	
	
	Набор = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
	Набор.Отбор.Штрихкод.Установить(Штрихкод);
	Набор.Прочитать();
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		Запись.Штрихкод = Штрихкод;
		Запись.Номенклатура = Номенклатура;
		Набор.Записать();
		ОписаниеОшибки = "(0)Для товара """+Номенклатура+""" установлен штрихкод """+СокрЛП(Штрихкод)+"""";
	Иначе
		Если Набор[0].Номенклатура<>Номенклатура Тогда
			ОписаниеОшибки = "(1)Штрихкод """+СокрЛП(Штрихкод)+""" уже назначен для товара """+Набор[0].Номенклатура+"""";
		КонецЕсли;	
	КонецЕсли;
	Возврат ОписаниеОшибки;
КонецФункции	

///////////////////////////////////////

&НаСервере
Функция ВывестиПротокол(МассивОшибок)
	
	Макет   = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	ОблНорм = Макет.ПолучитьОбласть("ОблНорм");
	ОблОш   = Макет.ПолучитьОбласть("ОблОш");
	
	Для каждого Эл из МассивОшибок Цикл
		ОблТек = ?(Лев(Эл,3) = "(0)", ОблНорм, ОблОш);
		ОблТек.Параметры.ОписаниеОшибки = Сред(Эл, 4);
		ТабДок.Вывести(ОблТек);
	КонецЦикла;
	
	ОблНорм.Параметры.ОписаниеОшибки = "Обработка завершена.";
	ТабДок.Вывести(ОблНорм);
	
КонецФункции

&НаСервере
Процедура ОчиститьТабДок()
	ТабДок.Очистить();
КонецПроцедуры
