
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресНастроекВнешнейОбработки = Параметры.АдресНастроекВнешнейОбработки;
	НастройкиВнешнейОбработки = ПолучитьИзВременногоХранилища(АдресНастроекВнешнейОбработки);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ОписаниеДействияВнешнейОбработки = Обработка.ОписаниеДействия(НастройкиВнешнейОбработки);
	Автонаименование = Обработка.Автонаименование(НастройкиВнешнейОбработки);
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Настройки условия: %1'"), Автонаименование);
	
	Если НастройкиВнешнейОбработки <> Неопределено Тогда
		
		Попытка
			Процент = НастройкиВнешнейОбработки.Процент;
		Исключение
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеДействияВнешнейОбработки);
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если Не ЗакрытьФормуПринудительно
		И Модифицированность Тогда
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru = 'Закрыть'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru = 'Не закрывать'"));
		
		Отказ = Истина;
		
		ДополнительныеПараметры = Новый Структура;
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПоказатьВопросПередЗакрытиемЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			НСтр("ru = 'Настройки были изменены. Закрыть форму без сохранения настроек?'"),
			СписокКнопок);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьНастройки(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	Если Процент = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не введен процент предоплаты"),,"Процент",, Отказ);
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗакрытьФормуПринудительно = Истина;
	Закрыть(СохранитьНастройкиВнешнейОбработки());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПоказатьВопросПередЗакрытиемЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = "Закрыть" Тогда
		ЗакрытьФормуПринудительно = Истина;
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиВнешнейОбработки()
	
	НастройкиВнешнейОбработки = Новый Структура;
	НастройкиВнешнейОбработки.Вставить("Процент", Процент);
	
	Обработка = РеквизитФормыВЗначение("Объект");
	ОписаниеДействияВнешнейОбработки = Обработка.ОписаниеДействия(НастройкиВнешнейОбработки);
	АвтонаименованиеВнешнейОбработки = Обработка.Автонаименование(НастройкиВнешнейОбработки);
	
	Возврат Новый Структура("ОписаниеДействияВнешнейОбработки, АдресНастроекВнешнейОбработки, АвтонаименованиеВнешнейОбработки",
		ОписаниеДействияВнешнейОбработки,
		ПоместитьВоВременноеХранилище(НастройкиВнешнейОбработки, АдресНастроекВнешнейОбработки),
		АвтонаименованиеВнешнейОбработки);
	
КонецФункции

#КонецОбласти