////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Возвращает сведения о внешней обработке
Функция СведенияОВнешнейОбработке() Экспорт
	
	Возврат СкидкиНаценкиСервер.ПараметрыРегистрацииВнешнейОбработкиУсловияПредоставленияСкидокНаценок(
		НСтр("ru = 'За план продаж'"),
		НСтр("ru = 'Условие выполняется, если клиент выполнил план продаж.'"));
		
КонецФункции

// Возвращает запрос для проверки условия предоставления (Проверка осуществляется запросом)
// Результат запроса - поле КратностьВыполнения, Число
//
// Параметры:
// Настройки - Структура - Настройки, заданные пользователем в форме настроек внешней обработки
//	ПараметрыРасчета - Структура - Параметры расчета скидок (наценок)
//
// Возвращаемое значение:
//	Запрос (обязательное поле: КратностьВыполнения, Число)
//	-1: Условие выполнено, но не влияет на результирующую кратность
//	0: Условие не выполнено
//	> 1: Условие выполнено, влияет на результирующую кратность
//
Функция Запрос(Настройки, ПараметрыРасчета) Экспорт
		
	ЗапросПроверка = Новый Запрос;
	//Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ЗапросПроверка.Текст =
		"ВЫБРАТЬ
		|	ЗаказКлиента.Партнер КАК Партнер,
		|	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента
		|ПОМЕСТИТЬ втДанныеЗаказаКлиента
		|ИЗ
		|	РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО ((ВЫРАЗИТЬ(СостоянияЗаказовКлиентов.Заказ КАК Документ.ЗаказКлиента)) = ЗаказКлиента.Ссылка)
		|ГДЕ
		|	ЗаказКлиента.Партнер = &Партнер
		|	И ЗаказКлиента.Проведен
		|	И СостоянияЗаказовКлиентов.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияЗаказовКлиентов.Закрыт)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	-1 КАК КратностьВыполнения
		|ИЗ
		|	РегистрСведений.КСП_ПланыПродаж КАК КСП_ПланыПродаж
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втДанныеЗаказаКлиента КАК втДанныеЗаказаКлиента
		|		ПО КСП_ПланыПродаж.Клиент = втДанныеЗаказаКлиента.Партнер
		|
		|ИМЕЮЩИЕ
		|	СУММА(КСП_ПланыПродаж.План) < СУММА(втДанныеЗаказаКлиента.СуммаДокумента)";
	
	ЗапросПроверка.УстановитьПараметр("Партнер", ПараметрыРасчета.Объект.Партнер);
	
	Выборка = ЗапросПроверка.Выполнить().Выбрать();
	
	Результат = Ложь;
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.КратностьВыполнения = -1;
	КонецЕсли;

	// Почему-то если отправить предыдцщий запрос результат всегда будет отрицательным
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	-1 КАК КратностьВыполнения
		|ГДЕ	
		|	&Результат = ИСТИНА");
	Запрос.УстановитьПараметр("Результат", Результат);
	
	Возврат Запрос;
	
КонецФункции

// Выполняет проверку выполнения условия предоставления (Проверка осуществляется алгоритмически)
//
// Параметры:
//	Настройки - Структура - Настройки, заданные пользователем в форме настроек внешней обработки
//	ПараметрыРасчета - Структура - Параметры расчета скидок (наценок)
//
// Возвращаемое значение:
//	Число - Кратность выполнения условия
//	-1: Условие выполнено, но не влияет на результирующую кратность
//	0: Условие не выполнено
//	> 1: Условие выполнено, влияет на результирующую кратность
//
Функция ПроверитьУсловие(Настройки, ПараметрыРасчета) Экспорт
	
	Возврат -1;
	
КонецФункции

// Возвращает имя формы внешней обработки,
// предназначенной для настройки условия предоставления 
//
// Возвращаемое значение:
//	Строка - Имя формы
//
Функция ИмяФормыНастроек() Экспорт
	
	Возврат "";
	
КонецФункции

// Возвращает расширенное описание условия предоставления с учетом настроек
//
// Параметры:
//	Настройки - Структура - Настройки, заданные пользователем в форме настроек внешней обработки
//
// Возвращаемое значение:
//	Строка - Расширенное описание
//
Функция ОписаниеДействия(Настройки) Экспорт
	
	Описание = НСтр("ru = 'Условие выполняется, если клиент выполнил план продаж.'");	
	Возврат Описание;
	
КонецФункции

// Возвращает заголовок условия предоставления с учетом настроек
//
// Параметры:
//	Настройки - Структура - Настройки, заданные пользователем в форме настроек внешней обработки
//
// Возвращаемое значение:
//	Строка - Наименование условия
//
Функция Автонаименование(Настройки) Экспорт
	
	Возврат НСтр("ru = 'За план продаж'");
	
КонецФункции

