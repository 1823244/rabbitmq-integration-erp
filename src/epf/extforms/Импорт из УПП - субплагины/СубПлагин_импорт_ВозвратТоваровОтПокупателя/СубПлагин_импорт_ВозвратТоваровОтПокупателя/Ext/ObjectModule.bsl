
Перем НЕ_ЗАГРУЖАТЬ;
Перем СОЗДАТЬ;
Перем ОБНОВИТЬ;
Перем ОТМЕНИТЬ_ПРОВЕДЕНИЕ;
Перем ПОМЕТИТЬ;

Перем мВнешняяСистема;
Перем ИмяСобытияЖР;
Перем jsonText;
Перем СобиратьНенайденнуюНоменклатуру Экспорт;
Перем НеНайденнаяНоменклатураМассив;

Перем мЛоггер;
Перем мИдВызова;


#Область ПодключениеОбработкиКБСП

Функция СведенияОВнешнейОбработке() Экспорт
	
	ПараметрыРегистрации = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	
	ПараметрыРегистрации.Вставить("Вид",ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиДополнительнаяОбработка());
	ПараметрыРегистрации.Вставить("Версия","1.10");
	//ПараметрыРегистрации.Вставить("Назначение", Новый Массив);
	ПараметрыРегистрации.Вставить("Наименование","СубПлагин_импорт_ВозвратТоваровОтПокупателя");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", Ложь);
	ПараметрыРегистрации.Вставить("Информация","СубПлагин_импорт_ВозвратТоваровОтПокупателя");
	ПараметрыРегистрации.Вставить("ВерсияБСП", "3.1.5.180");
	//ПараметрыРегистрации.Вставить("ОпределитьНастройкиФормы", Ложь);
	
	ТипКоманды = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыОткрытиеФормы();
	ДобавитьКоманду(ПараметрыРегистрации.Команды, 
	"Открыть форму : СубПлагин_импорт_ВозвратТоваровОтПокупателя",
	"Форма_СубПлагин_импорт_ВозвратТоваровОтПокупателя",
	ТипКоманды, 
	Ложь) ;
	
	
	Возврат ПараметрыРегистрации;
	
КонецФункции

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "")
	НоваяКоманда = ТаблицаКоманд.Добавить() ;
	НоваяКоманда.Представление = Представление ;
	НоваяКоманда.Идентификатор = Идентификатор ;
	НоваяКоманда.Использование = Использование ;
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение ;
	НоваяКоманда.Модификатор = Модификатор ;
КонецПроцедуры

#КонецОбласти 	


#Область ЗагрузитьОбъект_

Функция ЗагрузитьОбъект(СтруктураОбъекта, jsonText = "", СкладЕРП = Неопределено) Экспорт
	
	мЛоггер = Вычислить("мис_ЛоггерСервер.getLogger(мИдВызова, ""Импорт документов из УПП: ВозвратТоваровОтПокупателя (УПП)"")");
	
	мЛоггер.инфо("Субплагин. Импорт док. УПП ВозвратТоваровОтПокупателя №"+СтруктураОбъекта.definition.Number);
	
	Попытка
		
		Если НЕ СтруктураОбъекта.Свойство("type") Тогда // на случай, если передадут пустой объект
			Возврат Неопределено;
		КонецЕсли;
		Если НЕ НРег(СтруктураОбъекта.type) = НРег("Документ.ВозвратТоваровОтПокупателя") Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		ИмяСобытияЖР = "Импорт_из_RabbitMQ_УПП";
		
		id = СтруктураОбъекта.identification;
		деф = СтруктураОбъекта.definition;
		
		Результат = Новый Структура;
		ВозвратТоваровОтПокупателя = СоздатьОбновитьДокументВозвратОтПокупателя(СтруктураОбъекта, СкладЕРП);
		Результат.Вставить("ВозвратТоваровОтПокупателя", ВозвратТоваровОтПокупателя);
		
		
		//*************************** Экспорт ненайденной номенклатуры ****************
		Попытка
			ксп_ЭкспортСлужебный.ВыполнитьЭкспорт_ГуидовНоменклатуры(НеНайденнаяНоменклатураМассив);
			Сообщить("Выполнен экспорт ненайденной номенклатуры - " + Строка(НеНайденнаяНоменклатураМассив.Количество()) + " позиций");
		Исключение
			ТекстОшибки = ОписаниеОшибки();
			Сообщить("Ошибка экспорта ненайденной номенклатуры в УПП.");
			ЗаписьЖурналаРегистрации(ИмяСобытияЖР, УровеньЖурналаРегистрации.Предупреждение,,,
				"Ошибка экспорта ненайденной номенклатуры в УПП. Подробности: " + ТекстОшибки);
		КонецПопытки;
		//***************************
		
		Возврат Результат;
		
	Исключение
		
		мЛоггер.ерр("Плагин: Импорт Документ.ВозвратТоваровОтПокупателя Подробности: "+ОписаниеОшибки());
		ВызватьИсключение;// для помещения в retry
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти


#Область Схема_1

&НаСервере
Функция СоздатьОбновитьДокументВозвратОтПокупателя(СтруктураОбъекта, СкладЕРП)
	
	// ВозвратТоваровОтПокупателя
	
	id = СтруктураОбъекта.identification;
	деф = СтруктураОбъекта.definition;
	
	ПредставлениеДокументаУПП = "ВозвратТоваровОтПокупателя (УПП) № " + строка(деф.number) + " от " + Строка(деф.date);
	
	УИД = Новый УникальныйИдентификатор(id.Ref);
	СуществующийДокСсылка = Документы.ВозвратТоваровОтКлиента.ПолучитьСсылку(УИД);
	
	ОбъектДанныхСсылка = СуществующийДокСсылка;
	
	Если ЗначениеЗаполнено(СуществующийДокСсылка.ВерсияДанных) Тогда
		ЭтоНовый = Ложь;
	Иначе
		ЭтоНовый = Истина;
	КонецЕсли;
	
	// -------------------------------------------- БЛОКИРОВКА
	
	Если НЕ ЭтоНовый Тогда
		МассивСсылок = Новый Массив;
		МассивСсылок.Добавить(СуществующийДокСсылка);
		Блокировка = ксп_Блокировки.СоздатьБлокировкуНесколькихОбъектов(МассивСсылок);
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Если НЕ ЭтоНовый Тогда
		Попытка
			Блокировка.Заблокировать();
		Исключение
			ТекстСообщения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ИмяСобытияЖР,УровеньЖурналаРегистрации.Ошибка,,,
			"Объект не загружен! Ошибка блокировки цепочки документов для " + ПредставлениеДокументаУПП + ". Подробности: " + ТекстСообщения);
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		
		Действие = ксп_ИмпортСлужебный.ДействиеСДокументом(ЭтоНовый, СуществующийДокСсылка, деф);
		
		Если Действие = НЕ_ЗАГРУЖАТЬ Тогда
			ОтменитьТранзакцию();
			мЛоггер.инфо("Действие = НЕ Загружать. Документ пропущен: %1", ПредставлениеДокументаУПП);
			Возврат СуществующийДокСсылка;
		КонецЕсли;
		
        // добавлено 2024-07-03
        Если Действие = ПОМЕТИТЬ Тогда
            ОбъектДанных = СуществующийДокСсылка.ПолучитьОбъект();
            ОбъектДанных.УстановитьПометкуУдаления(Истина);
            РегистрыСведений.ксп_ОтложенноеПроведение.УдалитьОтложенноеПроведение(СуществующийДокСсылка);//добавлено 2024-07-03
            ЗафиксироватьТранзакцию();
			мЛоггер.дебаг("Действие = ПОМЕТИТЬ. Документ будет помечен на удаление: %1", ПредставлениеДокументаУПП);
			Возврат СуществующийДокСсылка;
		КонецЕсли;
		
		Если Действие = ОТМЕНИТЬ_ПРОВЕДЕНИЕ Тогда
			ОбъектДанных = СуществующийДокСсылка.ПолучитьОбъект();
			ОбъектДанных.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			ЗафиксироватьТранзакцию();
			мЛоггер.инфо("Действие = Отменить проведение. Документ: %1", ПредставлениеДокументаУПП);
			Возврат СуществующийДокСсылка;
		КонецЕсли;
		
		Если Действие = ОБНОВИТЬ Тогда
			ОбъектДанных = СуществующийДокСсылка.ПолучитьОбъект();
			мЛоггер.инфо("Действие = Обновить. Документ будет обновлен: %1", ПредставлениеДокументаУПП);
		ИначеЕсли Действие = СОЗДАТЬ Тогда
			ОбъектДанных = Документы.ВозвратТоваровОтКлиента.СоздатьДокумент();
			СсылкаНового = Документы.ВозвратТоваровОтКлиента.ПолучитьСсылку(УИД);
			ОбъектДанных.УстановитьСсылкуНового(СсылкаНового);
			мЛоггер.инфо("Действие = Создать. Документ будет создан: %1", ПредставлениеДокументаУПП);
			ОбъектДанныхСсылка = СсылкаНового;
		Иначе 
			ОтменитьТранзакцию();
			ТекстСообщения = "Действие = Неизвестое действие: "+Строка(Действие)+". Документ: " + ПредставлениеДокументаУПП;
			мЛоггер.ерр(ТекстСообщения);
			
			ВызватьИсключение ТекстСообщения;
			
		КонецЕсли;
		
		//------------------------------------- Заполнение реквизитов -----------------------------------
		
		ОбъектДанных.Номер = деф.Number;
		ОбъектДанных.Дата = деф.Date;
		//ОбъектДанных.ПометкаУдаления = деф.DeletionMark;




		кодВалюты="";
		ЕстьАтрибут = деф.ВалютаДокумента.свойство("currencyCode",кодВалюты);
		Если ЕстьАтрибут Тогда
			ОбъектДанных.Валюта = ксп_ИмпортСлужебный.НайтиВалюту(кодВалюты);
		КонецЕсли;

		//_знч = "";
		//ЕстьЗначение = деф.ВариантПриемкиТоваров.свойство("Значение",_знч);
		//Если ЕстьЗначение Тогда
		//	ОбъектДанных.ВариантПриемкиТоваров = деф.ВариантПриемкиТоваров.Значение;
		//Иначе
		//	ОбъектДанных.ВариантПриемкиТоваров = Неопределено;
		//КонецЕсли;
		//// на случай, если есть метод поиска ссылки:
		ОбъектДанных.ВариантПриемкиТоваров = Перечисления.ВариантыПриемкиТоваров.РазделенаПоЗаказамИНакладным;


		Контрагент = ксп_ИмпортСлужебный.НайтиКонтрагента(деф.Контрагент, мВнешняяСистема);
		УзелКонтрагента = Неопределено;
		ДоговорКонтрагента = ксп_ИмпортСлужебный.НайтиДоговор(деф.ДоговорКонтрагента, УзелКонтрагента, Контрагент);
		
		ОбъектДанных.Договор = ДоговорКонтрагента;

		ОбъектДанных.Комментарий = СтрШаблон("[УПП ВозвратТоваровОтПокупателя № %1 от %2 ]
		|Склад: %3
		|Оригинальный комментарий: %4",
		строка(деф.number),
		строка(деф.date),
		СкладЕРП,
		деф.Комментарий,);

		ОбъектДанных.Контрагент = Контрагент;

		ОбъектДанных.Организация = ксп_ИмпортСлужебный.НайтиОрганизацию(деф.Организация, мВнешняяСистема);

		ОбъектДанных.Партнер = ОбъектДанных.Контрагент.Партнер;

		//гуид="";
		//ЕстьАтрибут = деф.Подразделение.свойство("Ref",гуид);
		//Если ЕстьАтрибут Тогда
		//	ОбъектДанных.Подразделение = Справочники_Документы.КакойТоВидМД.ПолучитьСсылку( Новый УникальныйИдентификатор( деф.Подразделение.Ref ) );
		//Иначе
		//	ОбъектДанных.Подразделение = Неопределено;
		//КонецЕсли;
		//// на случай, если есть метод поиска ссылки:
		//ОбъектДанных.Подразделение = ксп_ИмпортСлужебный.НайтиПодразделение(деф.Подразделение);

		ОбъектДанных.Склад = СкладЕРП;


   		ОбъектДанных.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратТоваровОтКлиента;

		ОбъектДанных.ЦенаВключаетНДС = деф.СуммаВключаетНДС;

        ОбъектДанных.СпособКомпенсации =   РегистрыСведений.ксп_ДополнительныеНастройкиИнтеграций.Настройка("СпособКомпенсацииВозвратаОтПокупателя", мВнешняяСистема);
		
		ОбъектДанных.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
		
		ОбъектДанных.СуммаДокумента = деф.СуммаДокумента;
		
		ОбъектДанных.Соглашение =   РегистрыСведений.ксп_ДополнительныеНастройкиИнтеграций.Настройка("СоглашениеВозвратаОтПокупателя", мВнешняяСистема);
		
		////------------------------------------------------------     ТЧ Товары



		ОбъектДанных.Товары.Очистить();


		Для счТовары = 0 По деф.ТЧТовары.Количество()-1 Цикл
			стрк = деф.ТЧТовары[счТовары];
			СтрокаТЧ = ОбъектДанных.Товары.Добавить();

		

			СтрокаТЧ.Номенклатура = ксп_ИмпортСлужебный.НайтиНоменклатуру(стрк.Номенклатура);
			СтрокаТЧ.Количество = стрк.Количество;
			СтрокаТЧ.КоличествоУпаковок = стрк.Количество;
			СтрокаТЧ.Характеристика = ксп_ИмпортСлужебный.НайтиХарактеристику(стрк.ХарактеристикаНоменклатуры);
			СтрокаТЧ.Упаковка = ксп_ИмпортСлужебный.НайтиЕдиницуИзмерения(стрк.ЕдиницаИзмерения, стрк.Номенклатура);
			
			СтрокаТЧ.СтавкаНДС = ксп_ИмпортСлужебный.ОпределитьСтавкуНДСПоПеречислениюУПП(стрк.СтавкаНДС);
			СтрокаТЧ.Цена = стрк.Цена;
			СтрокаТЧ.СуммаНДС = стрк.СуммаНДС;
			СтрокаТЧ.Сумма = стрк.Сумма;
			
			Если деф.СуммаВключаетНДС = Истина Тогда
				СтрокаТЧ.СуммаСНДС = стрк.Сумма;
			Иначе 
				СтрокаТЧ.СуммаСНДС = стрк.Сумма + стрк.СуммаНДС;
			КонецЕсли;
			
			СтрокаТЧ.Себестоимость = стрк.Себестоимость;
			СтрокаТЧ.ШтрихКод = стрк.ШтрихКод;
		

		КонецЦикла;
		
		
     	ОбъектДанных.ОбъектРасчетов = СоздатьОбновитьОбъектРасчетов(ОбъектДанных, id, ОбъектДанныхСсылка);		
			
		ОбъектДанных.ОбменДанными.Загрузка = Ложь;
		ОбъектДанных.ДополнительныеСвойства.Вставить("НеРегистрироватьКОбменуRabbitMQ", Истина);
		Если ОбъектДанных.Проведен Тогда
			ОбъектДанных.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе 
			ОбъектДанных.Записать();
		КонецЕсли;     
		
		
		
		
		
		// Документ будет помещен в Отложенное проведение
		ксп_ИмпортСлужебный.Документы_ПослеИмпорта_01(ОбъектДанных, мВнешняяСистема, СтруктураОбъекта, jsonText, ЭтотОбъект);
		ксп_ИмпортСлужебный.Документы_ПослеИмпорта_02(ОбъектДанных, мВнешняяСистема, СтруктураОбъекта, jsonText, ЭтотОбъект);
		ксп_ИмпортСлужебный.Документы_ПослеИмпорта_03(ОбъектДанных, мВнешняяСистема, СтруктураОбъекта, jsonText, ЭтотОбъект);
		ксп_ИмпортСлужебный.Документы_ПослеИмпорта_04(ОбъектДанных, мВнешняяСистема, СтруктураОбъекта, jsonText, ЭтотОбъект);
		ксп_ИмпортСлужебный.Документы_ПослеИмпорта_05(ОбъектДанных, мВнешняяСистема, СтруктураОбъекта, jsonText, ЭтотОбъект);
		
		ЗафиксироватьТранзакцию();
		
		мЛоггер.инфо("Записан Документ : %1. Исходный док. УПП: %2", ОбъектДанных, ПредставлениеДокументаУПП);
		
		Возврат ОбъектДанных.Ссылка;
		
	Исключение
		
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИмяСобытияЖР,УровеньЖурналаРегистрации.Ошибка,,,
			"Объект не загружен! Ошибка в процессе загрузки документа " + ПредставлениеДокументаУПП + ". Подробности: " + СообщениеОбОшибке);
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		
		мЛоггер.ерр("Ошибка загрузки документа (УПП): %1. Подробности: %2", ПредставлениеДокументаУПП, СообщениеОбОшибке);
		
		ВызватьИсключение;
		
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции   


// Описание_метода
//
// Параметры:
//	Параметр1 	- Тип1 - 
//
// Возвращаемое значение:
//	Тип: Тип_значения
//
Функция СоздатьОбновитьОбъектРасчетов(ОбъектДанных, id, ОбъектДанныхСсылка)
	// объект расчетов
	УИД = Новый УникальныйИдентификатор(id.Ref);
	ОбъектРасчетов = Справочники.ОбъектыРасчетов.ПолучитьСсылку(уид);
	Если НЕ ЗначениеЗаполнено(ОбъектРасчетов.ВерсияДанных) Тогда
		ОбъектРасчетовОбъект = Справочники.ОбъектыРасчетов.СоздатьЭлемент();

		СсылкаНового = Справочники.ОбъектыРасчетов.ПолучитьСсылку(уид);
		ОбъектРасчетовОбъект.УстановитьСсылкуНового(СсылкаНового);
		
	Иначе 
		ОбъектРасчетовОбъект = ОбъектРасчетов.ПолучитьОбъект();
	КонецЕсли;
	

	ОбъектРасчетовОбъект.Объект = ОбъектДанныхСсылка;

	
	
	
	//БанковскийСчетКонтрагента
	//БанковскийСчетОрганизации
	ОбъектРасчетовОбъект.Валюта = ОбъектДанных.Валюта;
	ОбъектРасчетовОбъект.ВалютаВзаиморасчетов = ОбъектДанных.Валюта;
	//ГруппаФинансовогоУчета
	ОбъектРасчетовОбъект.Дата = ОбъектДанных.Дата;
	//ДатаВходящегоДокумента
	//
	ОбъектРасчетовОбъект.Договор = ОбъектДанных.Договор;
	//ДополнительныеРеквизиты
	//ИдентификаторПлатежа
	//ИмяПредопределенныхДанных
	//Касса
	//Комментарий
	//
	ОбъектРасчетовОбъект.Контрагент = ОбъектДанных.Контрагент;
	//Менеджер
	ОбъектРасчетовОбъект.Наименование  = Строка(ОбъектДанных);
	//НаименованиеПервичногоДокумента
	ОбъектРасчетовОбъект.НалогообложениеНДС = ОбъектДанных.НалогообложениеНДС;
	//НаправлениеДеятельности
	//
	ОбъектРасчетовОбъект.Номер = ОбъектДанных.Номер;
	//НомерВходящегоДокумента
	//Объект
	//ОплатаВВалюте
	//
	ОбъектРасчетовОбъект.Организация = ОбъектДанных.Организация;
	//
	ОбъектРасчетовОбъект.Партнер = ОбъектДанных.Партнер;
	//Подразделение
	//ПометкаУдаления
	//Предопределенный
	//
	ОбъектРасчетовОбъект.Соглашение = ОбъектДанных.Соглашение;
	//Состояние
	//Ссылка
	ОбъектРасчетовОбъект.Сумма = ОбъектДанных.СуммаДокумента;
	//
	ОбъектРасчетовОбъект.СуммаВзаиморасчетов = ОбъектДанных.СуммаДокумента;
	ОбъектРасчетовОбъект.ТипОбъектаРасчетов = Перечисления.ТипыОбъектовРасчетов.ПлатежВозврат;
	ОбъектРасчетовОбъект.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом;
	ОбъектРасчетовОбъект.ТипСсылки = Справочники.ИдентификаторыОбъектовМетаданных.НайтиПоНаименованию("Возврат товаров от клиента (Документ)", Истина);
	//ТолькоОстатки
	ОбъектРасчетовОбъект.УникальныйИдентификатор = Строка(id.Ref);
	//ФормаОплаты	

	ОбъектРасчетовОбъект.Записать();

		
	Возврат ОбъектРасчетовОбъект.ссылка;
	
КонецФункции


#КонецОбласти 	


#Область Тестирование

// Описание_метода
//
// Параметры:
//	Параметр1 	- Тип1 - 
//
// Возвращаемое значение:
//	Тип: Тип_значения
//
Функция ЗагрузитьИзJsonНаСервере(Json, СкладЕРП) export
	
	мЧтениеJSON = Новый ЧтениеJSON;
	мЧтениеJSON.УстановитьСтроку(Json);
	СтруктураОбъекта = ПрочитатьJSON(мЧтениеJSON,,,,"ФункцияВосстановленияJSON",ЭтотОбъект);//структура
	
	Если ТипЗнч(СтруктураОбъекта) = Тип("Массив") Тогда
		
		Для каждого эл Из СтруктураОбъекта Цикл
			ЗагрузитьОбъект(эл,,СкладЕРП);
		КонецЦикла;
		
	Иначе 
		Возврат ЗагрузитьОбъект(СтруктураОбъекта, , СкладЕРП);
	КонецЕсли;
	
	
КонецФункции

#КонецОбласти 	


#Область Служебные

Функция ФункцияВосстановленияJSON(Свойство, Значение, ДопПараметры) Экспорт
	
	Если Свойство = "Date" Тогда
		Возврат ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	КонецЕсли;
	Если Свойство = "Период" Тогда
		Возврат ПрочитатьДатуJSON(Значение, ФорматДатыJSON.ISO);
	КонецЕсли;
	Если Свойство = "Сумма" Тогда
		Если ТипЗнч(Значение) = Тип("Число") Тогда
			Возврат Значение;
		Иначе
			Возврат XMLЗначение(Тип("Число"),Значение);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Описание_метода
//
// Параметры:
//	Параметр1 	- Тип1 - 
//
// Возвращаемое значение:
//	Тип: Тип_значения
//
Функция МассивРеквизитовШапкиДляПроверки() Экспорт
	
	мРеквизиты = Новый Массив;
	мРеквизиты.Добавить("Склад");
	Возврат мРеквизиты;
	
КонецФункции

// Используется в  ксп_ИмпортСлужебный.ПроверитьКачествоДанных()
//
// Параметры:
//  ДокументОбъект  - ДокументСсылка - <описание параметра>
//
// Возвращаемое значение:
//  ТЗ, Колонки:
//   * ИмяТЧ
//   * ИмяКолонки
//
Функция ТабличныеЧастиДляПроверки(ДокументСсылка = Неопределено) Экспорт
	
	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("ИмяТЧ");
	ТЗ.Колонки.Добавить("ИмяКолонки");
	
	Если ДокументСсылка = Неопределено 
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") Тогда
		
		НовСтр = ТЗ.Добавить();
		НовСтр.ИмяТЧ = "Товары";
		НовСтр.ИмяКолонки = "Номенклатура";
		НовСтр = ТЗ.Добавить();
		НовСтр.ИмяТЧ = "Товары";
		НовСтр.ИмяКолонки = "Характеристика";
		
	КонецЕсли;
	
	Возврат ТЗ;
	
КонецФункции

// Описание_метода
//
// Параметры:
//	ГУИД 	- строка - 36 симв
//
// Возвращаемое значение:
//	Тип: ДокументСсылка
//
Функция СоздатьПолучитьСсылкуДокумента(ГУИД, ВидОбъекта)
	
	СуществующийОбъект = Документы[ВидОбъекта].ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД));
	
	Если ЗначениеЗаполнено(СуществующийОбъект.ВерсияДанных) Тогда
		Возврат СуществующийОбъект;
	Иначе 
		
		ОбъектДанных = Документы[ВидОбъекта].СоздатьДокумент();
		СсылкаНового = Документы[ВидОбъекта].ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД));
		ОбъектДанных.УстановитьСсылкуНового(СсылкаНового);
		
		Возврат ОбъектДанных.Ссылка;
	КонецЕсли;
	
КонецФункции

// Описание_метода
//
// Параметры:
//	ГУИД 	- строка - 36 симв
//
// Возвращаемое значение:
//	Тип: Тип_значения
//
Функция ПолучитьСсылкуСправочника(ГУИД, ВидОбъекта)
	
	СуществующийОбъект = Справочники[ВидОбъекта].ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД));
	
	Если ЗначениеЗаполнено(СуществующийОбъект.ВерсияДанных) Тогда
		
		Возврат СуществующийОбъект;
		
	Иначе
		
		Возврат Справочники[ВидОбъекта].ПустаяСсылка();
		
	КонецЕсли;
	
КонецФункции

// Описание_метода
//
// Параметры:
//	ГУИД 	- строка - 36 симв
//	ВидОбъекта 	- строка - Наименование метеданных
//
// Возвращаемое значение:
//	Тип: Тип_значения
//
Функция СоздатьПолучитьСсылкуСправочника(ГУИД, ВидОбъекта) Экспорт
	
	СуществующийОбъект = Справочники[ВидОбъекта].ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД));
	
	Если ЗначениеЗаполнено(СуществующийОбъект.ВерсияДанных) Тогда
		Возврат СуществующийОбъект;
	Иначе
		
		ОбъектДанных = Справочники[ВидОбъекта].СоздатьЭлемент();
		СсылкаНового = Справочники[ВидОбъекта].ПолучитьСсылку(Новый УникальныйИдентификатор(ГУИД));
		ОбъектДанных.УстановитьСсылкуНового(СсылкаНового);
		
		Возврат ОбъектДанных.Ссылка;
	КонецЕсли;
	
КонецФункции

// Описание_метода
//
// Параметры:
//  Параметр1   - Тип1 -
//
Функция сетИдВызова(пИдВызова) Экспорт
	
	мИдВызова = пИдВызова;
	Возврат ЭтотОбъект;
	
КонецФункции

#КонецОбласти 	


мВнешняяСистема = "UPP";
ИмяСобытияЖР = "Импорт_из_RabbitMQ_УПП";
СобиратьНенайденнуюНоменклатуру = Истина;
НеНайденнаяНоменклатураМассив = Новый Массив;

НЕ_ЗАГРУЖАТЬ = 1;
СОЗДАТЬ = 2;
ОБНОВИТЬ = 3;
ОТМЕНИТЬ_ПРОВЕДЕНИЕ = 4;
ПОМЕТИТЬ = 5;
