	
	// ЕНС. 2024-02-19. AS-IS. Это то, что есть сейчас в демо-контуре Элис
	// Это обработчик на стороне 1C:UPP 1.2, direction = OUT (to Platform)
	// В этом направлении данный код выполняется обработкой
	//		"сшпОбработчикВDatareon"
	// т.е. всё происходит в контексте модуля объекта этой обработки

	/////////////////////////////////////////////////////////////////////////////

	// Далее - код обработчика

	// ВАЖНО! Код выполняется в УПП!

	ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбъектОбработки.ВидНоменклатуры, "ТипНоменклатуры"); 
Если НЕ ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар И НЕ ОбъектОбработки.ЭтоГруппа Тогда
	СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	Перейти ~Выход;
КонецЕсли;

ЗаполненВидПродукции = ЗначениеЗаполнено(ОбъектОбработки.ВидПродукции);
Если НЕ ОбъектОбработки.ЭтоГруппа И НЕ ЗаполненВидПродукции Тогда
	СостояниеСообщения = Перечисления.сшпСтатусыСообщений.ОбработкаОтменена;
	Перейти ~Выход;
КонецЕсли;

Локализация = "ru";
//РезультатОбработки.ClassId = 3;
//РезультатОбработки.ClassId = 5;

Данные = Новый Структура;
Данные.Вставить(НСтр("ru = 'Ссылка'; en = 'ID'", Локализация), ОбъектОбработки.Ссылка);
Данные.Вставить(НСтр("ru = 'ПометкаУдаления'; en = 'DeletionMark'", Локализация), ОбъектОбработки.ПометкаУдаления);
Данные.Вставить(НСтр("ru = 'ЭтоГруппа'; en = 'IsGroup'", Локализация), ОбъектОбработки.ЭтоГруппа);
Данные.Вставить(НСтр("ru = 'Родитель'; en = 'Parent'", Локализация), ОбъектОбработки.Родитель);
Данные.Вставить(НСтр("ru = 'Наименование'; en = 'Description'", Локализация), ОбъектОбработки.Наименование);
Данные.Вставить(НСтр("ru = 'Код'; en = 'Code'", Локализация), ОбъектОбработки.Код);
Данные.Вставить(НСтр("ru = 'ДатаРегистрации'; en = 'RegistrationDate'", Локализация), ДатаРегистрации);
Если Не ОбъектОбработки.ЭтоГруппа Тогда
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(Номенклатура.ВидПродукции.Код, """") КАК ВидПродукцииКод,
	|	ЕСТЬNULL(Номенклатура.КодТНВЭД.ОбязательнаяМТ, ЛОЖЬ) КАК ОбязательнаяМТ,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Номенклатура.ВнутреннийТовар ССЫЛКА Справочник.Артикул
	|				ТОГДА ВЫРАЗИТЬ(Номенклатура.ВнутреннийТовар КАК Справочник.Артикул).Владелец.Зарисовка.КлассВязальнойМашины
	|		КОНЕЦ, """") КАК КлассВязальнойМашины
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕдиницыИзмерения.Ссылка
	|ИЗ
	|	Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО (Номенклатура.ЕдиницаХраненияОстатков = ЕдиницыИзмерения.Ссылка)
	|			И ЕдиницыИзмерения.Владелец = Номенклатура.Ссылка
	|ГДЕ
	|	ЕдиницыИзмерения.Владелец = &Номенклатура
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Штрихкоды.Штрихкод КАК Штрихкод,
	|	Штрихкоды.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	Штрихкоды.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(ПартияСекции.Ссылка, ЗНАЧЕНИЕ(Справочник.ПартияСекции.ПустаяСсылка)) КАК Партия
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартияСекции КАК ПартияСекции
	|		ПО Штрихкоды.Секция = ПартияСекции.Секция
	|			И Штрихкоды.Владелец = ПартияСекции.Номенклатура
	|ГДЕ
	|	Штрихкоды.Владелец = &Номенклатура
	|	И Штрихкоды.ТипШтрихкода = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыШтрихкодов.EAN13)
	|	И НЕ Штрихкоды.Штрихкод = """"
	|	И НЕ Штрихкоды.ХарактеристикаНоменклатуры.Ссылка ЕСТЬ NULL
	|	И НЕ Штрихкоды.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И НЕ ЕСТЬNULL(Штрихкоды.Владелец.КодТНВЭД.ОбязательнаяМТ, ЛОЖЬ)
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПолученныеGTIN.GTIN,
	|	ПолученныеGTIN.ХарактеристикаНоменклатуры,
	|	ЕдиницыИзмерения.Ссылка,
	|	ЗНАЧЕНИЕ(Справочник.ПартияСекции.ПустаяСсылка)
	|ИЗ
	|	РегистрСведений.ПолученныеGTIN КАК ПолученныеGTIN
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЕдиницыИзмерения КАК ЕдиницыИзмерения
	|		ПО (ЕдиницыИзмерения.Коэффициент = 1)
	|			И ПолученныеGTIN.Номенклатура = ЕдиницыИзмерения.Владелец
	|ГДЕ
	|	ПолученныеGTIN.Номенклатура = &Номенклатура
	|	И ПолученныеGTIN.Номенклатура.КодТНВЭД.ОбязательнаяМТ
	|	И НЕ ПолученныеGTIN.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И НЕ ПолученныеGTIN.GTIN ПОДОБНО ""02%""
	|   И НЕ (ПолученныеGTIN.Номенклатура.Наименование ПОДОБНО (""IS%"") ИЛИ ПолученныеGTIN.Номенклатура.Наименование ПОДОБНО (""WS%""))
	|   
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫРАЗИТЬ(Штрихкоды.Штрихкод КАК СТРОКА(14)),
	|	Штрихкоды.ХарактеристикаНоменклатуры,
	|	Штрихкоды.ЕдиницаИзмерения,
	|	ЗНАЧЕНИЕ(Справочник.ПартияСекции.ПустаяСсылка)
	|ИЗ
	|	РегистрСведений.Штрихкоды КАК Штрихкоды
	|ГДЕ
	|	Штрихкоды.Владелец = &Номенклатура
	|	И Штрихкоды.ТипШтрихкода = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ТипыШтрихкодов.Code128)
	|	И НЕ Штрихкоды.Штрихкод = """"
	|	И НЕ Штрихкоды.ХарактеристикаНоменклатуры.Ссылка ЕСТЬ NULL
	|	И НЕ Штрихкоды.ХарактеристикаНоменклатуры = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	И ЕСТЬNULL(Штрихкоды.Владелец.КодТНВЭД.ОбязательнаяМТ, ЛОЖЬ)
	|	И (Штрихкоды.Владелец.Наименование ПОДОБНО ""IS%""
	|			ИЛИ Штрихкоды.Владелец.Наименование ПОДОБНО ""WS%"")
	|   И НЕ Штрихкоды.Штрихкод ПОДОБНО ""04620143914673%""
	|   И НЕ Штрихкоды.Штрихкод ПОДОБНО ""04620143914697%""
	|   И НЕ Штрихкоды.Штрихкод ПОДОБНО ""04620143914710%""
	|	И НЕ Штрихкоды.Штрихкод ПОДОБНО ""02%""";
	
	Запрос.УстановитьПараметр("Номенклатура", ОбъектОбработки.Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	тзДанныеНоменклатуры = Результат[0].Выгрузить();
	тзЕдиницыИзмерения = Результат[1].Выгрузить();
	тзШтрихКоды = Результат[2].Выгрузить();
	
	ВидПродукции = тзДанныеНоменклатуры[0].ВидПродукцииКод;
	// код свойства в WMS "МаркируемаяПродукция_15970319575a44bc894c5ffba9bec678"
	МаркируемаяПродукция = тзДанныеНоменклатуры[0].ОбязательнаяМТ;
	// код свойства в WMS "КлассВязальнойМашины_302c7be94b5c42d49f16f25b02bffd50"
	КлассВязальнойМашины = СокрЛП(тзДанныеНоменклатуры[0].КлассВязальнойМашины);
	
	ВидКоллекции = "";
	Если ЗначениеЗаполнено(ОбъектОбработки.ВидКоллекции) Тогда
		ВидКоллекции = Перечисления.ВидыКоллекций.Индекс(ОбъектОбработки.ВидКоллекции);
	КонецЕсли;

	ТорговаяМарка = "";
	Если ЗначениеЗаполнено(ОбъектОбработки.ТорговаяМарка) Тогда
		ОбъектМетаданных = ОбъектОбработки.ТорговаяМарка.Метаданные();
		Индекс = Перечисления[ОбъектМетаданных.Имя].Индекс(ОбъектОбработки.ТорговаяМарка);
		ТорговаяМарка = ОбъектМетаданных.ЗначенияПеречисления[Индекс].Имя;
		// Костыль. Меня заставили
		Если ТорговаяМарка = "ElisMen" Тогда
			ТорговаяМарка = "20line";
		КонецЕсли;
	КонецЕсли;

	Данные.Вставить(НСтр("ru = 'Артикул'; en = 'Article'", Локализация), ОбъектОбработки.Артикул);
	Данные.Вставить(НСтр("ru = 'ВидНоменклатуры'; en = 'ItemType'", Локализация), ОбъектОбработки.ВидНоменклатуры);
	Данные.Вставить(НСтр("ru = 'ЕдиницаИзмерения'; en = 'UnitOfMeasure'", Локализация), ОбъектОбработки.БазоваяЕдиницаИзмерения);
	Данные.Вставить(НСтр("ru = 'Комментарий'; en = 'Comment'", Локализация), Строка(ОбъектОбработки.ПериодВывески));
	Данные.Вставить(НСтр("ru = 'Масса'; en = 'Weight'", Локализация), 0);
	Данные.Вставить(НСтр("ru = 'НаименованиеПолное'; en = 'LongDescription'", Локализация), ОбъектОбработки.НаименованиеПолное);
	Данные.Вставить(НСтр("ru = 'Объем'; en = 'Volume'", Локализация), 0);
	Данные.Вставить(НСтр("ru = 'ЕдиницаИзмеренияСрокаХранения'; en = 'UnitOfMeasureForShelfLife'", Локализация), "");
	Данные.Вставить(НСтр("ru = 'СрокХранения'; en = 'ShelfLife'", Локализация), "");
	Данные.Вставить(НСтр("ru = 'НаборУпаковок'; en = 'SetOfPackages'", Локализация), "");
	Данные.Вставить(НСтр("ru = 'ЯвляетсяКомплектом'; en = 'IsKit'", Локализация), Ложь);
	Данные.Вставить(НСтр("ru = 'ВидПродукции'; en = 'KindOfProduction'", Локализация), ВидПродукции);
	Данные.Вставить(НСтр("ru = 'ВидКоллекции'; en = 'KindOfCollection'", Локализация), ВидКоллекции);
	Данные.Вставить(НСтр("ru = 'МаркируемаяПродукция'; en = 'IsMarkedItem'", Локализация), МаркируемаяПродукция);
	Данные.Вставить(НСтр("ru = 'ТорговаяМарка'; en = 'TradeMark'", Локализация), ТорговаяМарка);
	Данные.Вставить(НСтр("ru = 'КлассВязальнойМашины'; en = 'KnittingMachineClass'", Локализация), КлассВязальнойМашины);

	//Упаковки
	мсвТаблица = Новый Массив;
	Для каждого Выборка Из тзЕдиницыИзмерения Цикл
		СтруктураУпаковки = Новый Структура;
		СтруктураУпаковки.Вставить(НСтр("ru = 'Ссылка'; en = 'ID'", Локализация), Выборка.Ссылка);
		мсвТаблица.Добавить(СтруктураУпаковки);
	КонецЦикла;
	Данные.Вставить(НСтр("ru = 'Упаковки'; en = 'ItemPackages'", Локализация), мсвТаблица);	

	// Курмак А.В. ++ 16.10.2023 г.
	СлужебныеПартии = Справочники.ПартияСекции.ПолучитьСлужебныеПартииСекцийНоменклатуры(ОбъектОбработки.Ссылка);
	// Курмак А.В. ++

	//Штрихкоды
	мсвТаблица = Новый Массив;
	Для каждого Выборка Из тзШтрихКоды Цикл
		СтруктураШтрихкода = Новый Структура;
		СтруктураШтрихкода.Вставить(НСтр("ru = 'Характеристика'; en = 'ItemCharacteristic'", Локализация), Выборка.ХарактеристикаНоменклатуры);
		СтруктураШтрихкода.Вставить(НСтр("ru = 'Упаковка'; en = 'ItemPackage'", Локализация), Выборка.ЕдиницаИзмерения);
		СтруктураШтрихкода.Вставить(НСтр("ru = 'Штрихкод'; en = 'Barcode'", Локализация), Выборка.Штрихкод);
		
		// Партию для покупного товара не выгружаем
		Если ОбъектОбработки.ВидВоспроизводства = Перечисления.ВидыВоспроизводстваНоменклатуры.Закупка Тогда
			СтруктураШтрихкода.Вставить(НСтр("ru = 'Партия'; en = 'ItemBatch'", Локализация), Справочники.ПартияСекции.ПустаяСсылка());
		Иначе
			СтруктураШтрихкода.Вставить(НСтр("ru = 'Партия'; en = 'ItemBatch'", Локализация), Выборка.Партия);
		КонецЕсли;
		мсвТаблица.Добавить(СтруктураШтрихкода);
	КонецЦикла;
	Данные.Вставить(НСтр("ru = 'Штрихкоды'; en = 'Barcodes'", Локализация), мсвТаблица);
	
КонецЕсли;

РезультатОбработки.Body = сшпОбщегоНазначения.ПреобразоватьСтруктуруПоФормату(ФорматСообщения, Данные);
~Выход: